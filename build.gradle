buildscript {
    repositories {
        maven {
            name 'MixinGradle'
            url 'https://repo.spongepowered.org/repository/maven-public'
        }
    }
    dependencies {
        if (project.use_mixins.toBoolean()) {
            classpath 'org.spongepowered:mixingradle:0.7.+'
        }
    }
}

plugins {
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'wtf.gofancy.fancygradle' version '1.1.+'
    id 'io.github.srdjan-v.local-git-dependency' version '0.1'
}

apply {
    plugin 'net.minecraftforge.gradle'
    plugin 'org.spongepowered.mixin'
    plugin "maven-publish"
    plugin 'java'
    plugin 'idea'
}

fancyGradle {
    patches {
        resources
        coremods
        asm
        codeChickenLib
    }
}

version = project.mod_version
group = project.maven_group
archivesBaseName = project.archives_base_name

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

minecraft {

    mappings channel: 'stable', version: '39-1.12'

    if (project.has_access_transformer.toBoolean()) {
        accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    }
    runs {
        client {
            workingDirectory project.file('run/client')
            if (project.use_coremod.toBoolean()) {
                jvmArg '-Dfml.coreMods.load=' + coremod_plugin_class_name
            }
            if (project.use_mixins.toBoolean()) {
                jvmArg '-Dmixin.hotSwap=true'
                jvmArg '-Dmixin.checks.interfaces=true'
                jvmArg '-Dmixin.debug=true'
            }
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            environment 'MC_VERSION', '1.12.2'
        }

        server {
            workingDirectory project.file('run/server')
            if (project.use_coremod.toBoolean()) {
                jvmArg '-Dfml.coreMods.load=' + coremod_plugin_class_name
            }
            if (project.use_mixins.toBoolean()) {
                jvmArg '-Dmixin.hotSwap=true'
                jvmArg '-Dmixin.checks.interfaces=true'
            }
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            environment 'MC_VERSION', '1.12.2'
        }
    }
}


repositories {
    maven {
        url = 'https://maven.cleanroommc.com'
    }
    maven {
        url = 'https://repo.spongepowered.org/maven'
    }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name 'jared maven'
        url 'https://maven.blamejared.com/'
        content {
            includeGroup "CraftTweaker2"
        }
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }

}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'

    implementation 'CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.20.679'
    implementation fg.deobf('curse.maven:ImmersiveEngineering-231951:2974106')
    implementation fg.deobf('curse.maven:ImmersivePetroleum-268250:3382321')

    //AdvancedRocketry
    implementation fg.deobf('curse.maven:AdvancedRocketry-236542:3801020')
    implementation fg.deobf('curse.maven:Libvulpes-236541:3801015')

    compileOnly fg.deobf('mezz.jei:jei_1.12.2:4.16.1.302:api')
    runtimeOnly fg.deobf('mezz.jei:jei_1.12.2:4.16.1.302')
    //implementation fg.deobf('curse.maven:had-enough-items-557549:3862733')

    compileOnly('zone.rong:mixinbooter:5.0')
    runtimeOnly('zone.rong:mixinbooter:5.0')
}

localGitDependency {
    add 'https://github.com/Srdjan-V/TweakedLib.git', {
        name 'TweakedLib'
        tag 'v' + project.tweaked_lib_version
    }
}

sourceSets {
    main {
        if (project.use_mixins.toBoolean()) {
            ext.refMap = 'mixins.' + archives_base_name + '.refmap.json'
        }
    }
}


jar {
    manifest {
        // noinspection GroovyAssignabilityCheck
        def attribute_map = [:]
        if (project.use_coremod.toBoolean()) {
            attribute_map['FMLCorePlugin'] = project.coremod_plugin_class_name
            if (project.include_mod.toBoolean()) {
                attribute_map['FMLCorePluginContainsFMLMod'] = true
                attribute_map['ForceLoadAsMod'] = project.gradle.startParameter.taskNames[0] == 'build'
            }
        }
        if (project.use_mixins.toBoolean()) {
            attribute_map['TweakClass'] = 'org.spongepowered.asm.launch.MixinTweaker'
        }
        attributes(attribute_map)
    }
}

jar.finalizedBy('reobfJar')


import org.apache.tools.ant.filters.ReplaceTokens

task filterTokens(type: Sync) {

    inputs.property 'version', version
    inputs.property 'tweaked_lib_version', tweaked_lib_version

    from sourceSets.main.java
    filter(ReplaceTokens, tokens: [VERSION: version.toString(), TWEAKEDLIB: tweaked_lib_version.toString()])
    into "$buildDir/src/$sourceSets.main.name/$sourceSets.main.java.name"
}

compileJava.source = filterTokens.outputs

processResources {
    // required to allow file expansion later
    duplicatesStrategy = 'include'

    // this will ensure that this task is redone when the versions change.
    inputs.property 'version', version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version
        expand 'version': version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

if (project.build_deobfJar.toBoolean()) {
    // Create deobf dev jars
    task deobfJar(type: Jar) {
        archiveClassifier.set("deobf")
        from sourceSets.main.output
    }
}

if (project.build_apiJar.toBoolean()) {
    // Create API library jar
    task apiZip(type: Jar) {
        archiveClassifier.set("api")
        from(sourceSets.main.java) {
            include "name/api/**"
        }
        from(sourceSets.main.output) {
            include "name/api/**"
        }
    }
}

if (project.build_sourceJar.toBoolean()) {
    // Create source jar
    task sourcesJar(type: Jar) {
        archiveClassifier.set("sources")
        from sourceSets.main.allJava
    }
}

artifacts {
    if (project.build_deobfJar.toBoolean()) {
        archives deobfJar
    }
    if (project.build_apiJar.toBoolean()) {
        archives apiZip
    }
    if (project.build_sourceJar.toBoolean()) {
        archives sourcesJar
    }
}

tasks.register('updateGitVersionTag') {
    doLast {
        def versionTagArray = new ArrayList<Integer>()
        for (versionString in project.version.toString().trim().split("\\.")) {
            versionTagArray.add(Integer.valueOf(versionString))
        }

        def gitVersionTagArray = new ArrayList<Integer>()
        def gitStringTag = ("git describe --tags --abbrev=0").execute().inputStream.readLines().get(0)
        for (versionString in gitStringTag.substring(1, gitStringTag.length()).trim().split("\\.")) {
            gitVersionTagArray.add(Integer.valueOf(versionString))
        }

        def invalid = true
        def smallestSizeArray = versionTagArray.size() > gitVersionTagArray.size() ? gitVersionTagArray.size() : versionTagArray.size()
        for (i in 0..<smallestSizeArray ) {
            if (versionTagArray.get(i) > gitVersionTagArray.get(i)) {
                invalid = false
                break
            }
        }

        if (!invalid && versionTagArray.size() > gitVersionTagArray.size()) {
            invalid = false
        }

        if (invalid) {
            project.logger.error("Current project version is the same or lower then the latest tag")
            return
        }

        project.logger.lifecycle('Pushing tag {}', project.version)
        ("git tag v${project.version}").execute()
        ("git push --tags").execute()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}